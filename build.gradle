plugins {
    id 'war'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
	
	// Java EE 8 und Microprofile
	compileOnly  group: 'javax', name: 'javaee-api', version: '8.0'
	compileOnly 'org.eclipse.microprofile:microprofile:4.1'
	
	// Commons... (aus Gründen der Build-/Deploy-Performance im lib-Verzeichnis vom Application-Server)
	compileOnly group: 'com.google.guava', name: 'guava', version: '28.1-jre'
	
	// Json Web Token (JWT)
	implementation 'com.auth0:java-jwt:3.18.3'
	
	// ---- TESTING ---------------------------------------------------------------------------------------------
	// JUnit5-Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation "org.junit.platform:junit-platform-suite:1.8.1"
        
	// Erweiterung: siehe http://joel-costigliola.github.io/assertj/assertj-core-quick-start.html
	testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.11.1'
	
	// Mockito für JUnit 5
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.25.1'
	
	// Commons
	testRuntimeOnly group: 'com.google.guava', name: 'guava', version: '23.0'
	
	// Bean-Validation
	testImplementation group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.0.13.Final'
	testImplementation group: 'org.glassfish', name: 'javax.el', version: '3.0.0'
    
    // JPA Integrationtest
	testImplementation (group: 'org.eclipse.persistence', name: 'eclipselink', version: '2.7.9') {
		exclude	group: 'org.eclipse.persistence', module: 'javax.persistence'
	}
	testImplementation group: 'javax.persistence', name: 'javax.persistence-api', version: '2.2'
	testImplementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.4'
	//testImplementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.22'
    
    // JSON
	testImplementation group: 'javax.json.bind', name: 'javax.json.bind-api', version: '1.0'
    testImplementation group: 'org.eclipse', name: 'yasson', version: '1.0.2'
    testImplementation  group: 'org.glassfish', name: 'javax.json', version: '1.1.4'
    testImplementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'
    // JAX RS
    testImplementation  group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.34'
    
    //testImplementation group: 'org.glassfish.jersey.core', name: 'jersey-common', version: '2.22.2'
   
}

test {
	useJUnitPlatform {
        includeTags("unit-test")
        includeEngines("junit-jupiter")
    }
}

task (dbtest, type: Test, group: 'verification') {
    description('Integrationstest gegen DB')
    dependsOn(test)
	useJUnitPlatform {
        includeTags("integration-test")
        includeEngines("junit-jupiter")
    }
}

// Deploy WAR in Autodeploy-Verzeichnis
task (deployWar, type: Copy, group: 'deploy') {
	dependsOn(war)
	from('./build/libs')
    into('./docker/autodeploy')
}

// Docker
String currentDir = './docker'

task (upDC, type: Exec, group: 'docker') {
    workingDir currentDir
    commandLine 'docker-compose','up', '-d'
}

task (downDC, type: Exec, group: 'docker') {
    workingDir currentDir
    commandLine 'docker-compose','down', '-t', '0'
}

task (restartDC, type: Exec, group: 'docker') {
    workingDir currentDir
    commandLine 'docker-compose','restart', '-t','0'
}
